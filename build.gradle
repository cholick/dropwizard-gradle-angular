apply plugin: 'groovy'
apply plugin: 'application'

sourceCompatibility = 1.7
targetCompatibility = 1.7

mainClassName = 'com.cholick.todo.App'
String springloadedVersion = '1.2.0.RELEASE'

repositories {
    mavenLocal()
    mavenCentral()
}

configurations {
    agent
}

dependencies {
    compile 'org.codehaus.groovy:groovy-all:2.3.4'

    compile 'com.google.inject:guice:3.0'

    compile 'io.dropwizard:dropwizard-core:0.7.1'
    compile 'io.dropwizard:dropwizard-assets:0.7.1'
    compile 'io.dropwizard:dropwizard-views:0.7.1'
    //jersey-client needed due to http://issues.gradle.org/browse/GRADLE-2647
    compile 'com.sun.jersey:jersey-client:1.18'

    testCompile 'org.spockframework:spock-core:0.7-groovy-2.0'
    testCompile 'io.dropwizard:dropwizard-testing:0.7.1'

    agent "org.springframework:springloaded:${springloadedVersion}"
}

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.bluepapa32:gradle-watch-plugin:0.1.3'
    }
}

run {
    args = ['server', 'app.yaml']
    //jvmArgs = ["-javaagent:${new File("$buildDir/agent/springloaded-${springloadedVersion}.jar").absolutePath}", '-noverify']
}

task runDev(type: JavaExec, dependsOn: classes) {
    args = ['server', 'app.yaml']
    main = mainClassName
    classpath = sourceSets.main.runtimeClasspath
    jvmArgs = ["-javaagent:${new File("$buildDir/agent/springloaded-${springloadedVersion}.jar").absolutePath}", '-noverify']
}

task copyAgent(type: Copy) {
    from configurations.agent
    into "$buildDir/agent"
}
runDev.mustRunAfter copyAgent

apply plugin: 'watch'
watch {
    groovy {
        files files('src/main/groovy')
        tasks 'compileGroovy'
    }
    assets {
        files files('src/main/resources')
        tasks 'processResources'
    }
}

task watchThread() << {
    Thread.start {
        project.tasks.watch.execute()
    }
}
runDev.mustRunAfter watchThread

task dev(dependsOn: [watchThread, copyAgent, runDev])
